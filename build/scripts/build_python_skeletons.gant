// Copyright 2000-2018 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.

import org.jetbrains.intellij.build.CompilationTasks
import org.jetbrains.intellij.build.impl.BuildUtils
import org.jetbrains.intellij.build.impl.CompilationContextImpl
import org.jetbrains.jps.idea.IdeaProjectLoader
import org.jetbrains.jps.model.java.JpsJavaExtensionService
import org.jetbrains.jps.model.library.JpsOrderRootType
import org.jetbrains.jps.util.JpsPathUtil

target('default': 'Generate python skeletons') {
  def home = IdeaProjectLoader.guessHome(this)
  def output = "$home/out/python_skeletons"
  def context = CompilationContextImpl.create(home, home, output)
  def moduleName = 'intellij.python.tools'

  CompilationTasks.create(context).with {
    resolveProjectDependencies()
    compileModules([moduleName])
  }
  (
    // dependencies
    context.findModule(moduleName)
      .with { JpsJavaExtensionService.dependencies(it) }
      .productionOnly().recursively()
      .libraries.collectMany { it.getRoots(JpsOrderRootType.COMPILED) }
      .collect { JpsPathUtil.urlToFile(it.url) } +
    // compiled modules and jars in lib directory
    ["$output/classes/production"]
      .collectMany { new File(it).listFiles().toList() }
      .findAll { it.isDirectory() || it.name.endsWith('.jar') }
  ).each {
    // com.intellij.util package gets loaded by AntClassLoader before this script starts
    // so we have to add dependencies to AntClassLoader classpath directly
    BuildUtils.addToJpsClassPath(it.absolutePath, ant)
    println("Adding to classpath: ${it.absolutePath}")
  }

  Class.forName('com.jetbrains.python.tools.BuildZippedSkeletonsKt').main()
}