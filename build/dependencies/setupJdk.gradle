import org.gradle.internal.os.OperatingSystem

import java.util.concurrent.TimeUnit

// Copyright 2000-2018 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
task setupJdks(dependsOn: ['setupJdk', 'checkJdk', 'setupJdk16'])
cleanSetupJdks.dependsOn('cleanSetupJdk', 'cleanSetupJdk16', 'cleanDownloadJdk')

project.ext.jdkRepo = 'https://cache-redirector.jetbrains.com/intellij-jdk'
project.ext.jdkVersion = System.getProperty('intellij.build.jdk.version', '8').toInteger()
project.ext.jdkArtifactPrefix = "jbsdk$jdkVersion"

def jdkDir = System.getProperty('intellij.build.jdks.target.dir')?.with {
  new File(it).mkdirs()
  it
} ?: "${intellijProjectDir()}/build/jdk"

project.ext.jdkOutputDir = System.getProperty("intellij.jdk.output.dir", "$jdkDir/${jdkVersion < 9 ? "1.$jdkVersion" : jdkVersion}")

task downloadJdk {
  def arch = jdkArch()
  def platform = currentPlatformName()
  def jdkUrl = jdkUrl()

  def artifactName = "${jdkArtifactPrefix}${jdkBuild}_${platform}_${arch}.tar.gz"
  def outputFile = "$project.buildDir/jbsdk/${artifactName}"

  inputs.property('version', jdkVersion)
  inputs.property('build', jdkBuild)
  inputs.property('url', jdkUrl)
  outputs.file(outputFile)
  doLast {
    logger.info("Downloading up JDK $jdkVersion to compile (using $jdkUrl)")
    download {
      src jdkUrl
      dest outputFile
      tempAndMove true
      timeout TimeUnit.MINUTES.toMillis(30).toInteger()
    }
  }
}

task setupJdk(dependsOn: downloadJdk) {
  def outputDir = jdkOutputDir
  def downloadOutputs = downloadJdk.outputs.files

  inputs.files(downloadOutputs)
  inputs.property("jdkOutputDir", outputDir)
  outputs.dir(outputDir)
  outputs.upToDateWhen {
    !fileTree(outputDir).isEmpty()
  }
  doLast {
    logger.info("Setting up JDK $jdkVersion to compile")
    unpackJdk(downloadOutputs.singleFile, outputDir)
  }
}

def unpackJdk(jdkArchive, outputDir) {
  logger.info("Unpacking $jdkArchive to ${outputDir}")
  if (file(outputDir).exists()) {
    delete(outputDir)
  }
  def currentOs = OperatingSystem.current()
  if (currentOs.isWindows()) {
    copy {
      from tarTree(jdkArchive)
      into outputDir
    }
  }
  else {
    exec { commandLine 'mkdir', '-p', outputDir }
    exec { commandLine 'tar', 'xpf', "$jdkArchive.absolutePath", '--directory', outputDir }
    if (currentOs.isMacOsX() && file("${outputDir}/jdk/Contents").exists()) {
      exec { commandLine 'mv', "${outputDir}/jdk/Contents", outputDir }
      exec { commandLine 'rm', '-rf', "${outputDir}/jdk" }
    }
  }
}

static def jdkArch() {
  def arch = System.getProperty("intellij.jdk.arch", "")
  if (arch == '32') return 'x86'
  if (arch == '64') return 'x64'
  def os = OperatingSystem.current()
  return !os.isWindows() || System.getenv("ProgramFiles(x86)") != null ? 'x64' : 'x86'
}

def jdkUrl() {
  def arch = jdkArch()
  def teamCityBuild = System.getProperty('intellij.jdk.teamcity.build')
  if (teamCityBuild != null) {
    def platform = OperatingSystem.current().with { os ->
      os.isWindows() ? 'win' : os.isMacOsX() ? 'mac' : 'linux'
    }
    "${teamCityBuild}/${platform}/${jdkArtifactPrefix}${jdkBuild}_${arch}.tar.gz"
  }
  else {
    def platform = currentPlatformName()
    "${jdkRepo}/${jdkArtifactPrefix}${jdkBuild}_${platform}_${arch}.tar.gz"
  }
}

static def currentPlatformName() {
  def os = OperatingSystem.current()
  if (os.isWindows()) {
    "windows"
  }
  else if (os.isMacOsX()) {
    "osx"
  }
  else {
    "linux"
  }
}

// JDK 1.6
repositories {
  maven {
    url 'http://repo.labs.intellij.net/java'
  }
}

configurations {
  jdk16
}

dependencies {
  jdk16 "jetbrains:jre:${jdk16Version()}@tar.gz"
}

task setupJdk16(dependsOn: configurations.jdk16) {
  if (!inJetBrainsNetwork) {
    return
  }

  def jdkArchive = { configurations.jdk16.singleFile }
  def outputDir = "$jdkDir/1.6"
  inputs.file(jdkArchive)
  outputs.dir(outputDir)

  doLast {
    logger.info("Setting up JDK 1.6 to compile")
    unpackJdk(jdkArchive.call(), outputDir)
    deleteBrokenSymlink(outputDir)
    createMissingLauncher(outputDir)
  }
}

// Gradle dies on trying to process broken links: https://issues.gradle.org/browse/GRADLE-1843
private void deleteBrokenSymlink(String outputDir) {
  if (OperatingSystem.current().isMacOsX()) {
    def pluginJar = file("$outputDir/Contents/Home/lib/plugin.jar")
    if (!pluginJar.exists()) {
      if (!pluginJar.delete()) {
        logger.info("Couldn't remove plugin.jar from JDK 1.6")
      }
    }
  }
}

// some JPS builders rely on a launcher presence in <JDK>/bin directory
private void createMissingLauncher(String outputDir) {
  if (!new File(outputDir, "bin").exists() && new File(outputDir, "jre/bin").exists()) {
    copy {
      from file(new File(outputDir, "jre/bin"))
      into file(new File(outputDir, "bin"))
      include 'java*'
    }
  }
}

setupJdk16.onlyIf { inJetBrainsNetwork }

def intellijProjectDir() {
  def projectDir = project.file("../..")
  if (projectDir.name == 'community') {
    while (new File(projectDir.parentFile, ".idea").exists() && !new File(projectDir.parentFile, '.idea/.notIdeaProject').exists()) {
      projectDir = projectDir.parentFile
    }
    return projectDir
  }
  return projectDir
}

static def jdk16Version() {
  def os = OperatingSystem.current()
  if (os.isWindows()) {
    def suffix = System.getenv("ProgramFiles(x86)") != null ? "x64" : "i586"
    return "6.45:windows_$suffix"
  }
  else if (os.isMacOsX()) {
    return '6.65:macosx_x64'
  }
  return '6.45:linux_x64'
}

task checkJdk(dependsOn: setupJdk) {
  doLast {
    def path = OperatingSystem.current().isMacOsX() ? "$jdkOutputDir/Contents/Home/bin" : "$jdkOutputDir/bin"
    def project = project
    new ByteArrayOutputStream().withStream { output ->
      project.exec {
        standardOutput = output
        errorOutput = output
        commandLine file("$path/java"), '-XshowSettings:properties', '-version'
      }
      String vendor = null
      String version = null
      output.toString().readLines().each {
        if (it.contains('java.vendor =')) {
          vendor = it
        }
        else if (it.contains('java.runtime.version =')) {
          version = it
        }
      }
      println(vendor)
      println(version)
      if (!vendor?.contains('JetBrains')) throw new IllegalStateException('java.vendor is not JetBrains')
      if (!version?.contains("$jdkVersion")) throw new IllegalStateException("java.runtime.version is not $jdkVersion")
    }
  }
}