// Copyright 2000-2021 JetBrains s.r.o. and contributors. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
configurations {
  rkernel
}

dependencies {
  rkernel "org.jetbrains.intellij.deps.rkernel:rwrapper:$rkernelVersion:x64-osx@tar.gz"
  rkernel "org.jetbrains.intellij.deps.rkernel:rwrapper:$rkernelVersion:x64-windows.exe@tar.gz"
  rkernel "org.jetbrains.intellij.deps.rkernel:rwrapper:$rkernelVersion:x64-linux@tar.gz"
}

task setupRkernel(dependsOn: configurations.rkernel) {
  group = 'rplugin'
  inputs.files(configurations.rkernel)
  def output = "$buildDir/rplugin"
  def version = file("$output/rkernelVersion.txt")
  outputs.dir(output)
  outputs.file(version)
  doLast {
    file("$output/rkernelVersion.txt").write("$rkernelVersion")
    copy {
      configurations.rkernel.files.each {
        from tarTree(it)
      }
      into("$buildDir/rplugin")
    }
  }
}

task copyFsNotifierForRPlugin {
  setupRkernel.dependsOn(it)
  group = setupRkernel.group
  outputs.file("$buildDir/rplugin/fsnotifier-linux")
  outputs.file("$buildDir/rplugin/fsnotifier-osx")
  outputs.file("$buildDir/rplugin/fsnotifier-win.exe")
  doLast {
    copy {
      from "$projectDir/../../bin/linux/"
      into "$buildDir/rplugin/"
      include "fsnotifier"
      rename "fsnotifier", "fsnotifier-linux"
    }
    copy {
      from "$projectDir/../../bin/mac/"
      into "$buildDir/rplugin/"
      include "fsnotifier"
      rename "fsnotifier", "fsnotifier-osx"
    }
    copy {
      from "$projectDir/../../bin/win/"
      into "$buildDir/rplugin/"
      include "fsnotifier.exe"
      rename "fsnotifier.exe", "fsnotifier-win.exe"
    }
  }
}
