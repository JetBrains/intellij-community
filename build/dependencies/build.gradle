// Copyright 2000-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
buildscript {
  repositories {
    maven { url "https://cache-redirector.jetbrains.com/plugins.gradle.org" }
  }
  dependencies {
    classpath "de.undercouch:gradle-download-task:4.1.1"
    classpath "gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:0.9"
  }
}

allprojects {
  repositories {
    maven { url 'https://cache-redirector.jetbrains.com/intellij-dependencies' }
  }
  apply plugin: 'idea'
  idea {
    module {
      inheritOutputDirs true
    }
  }
}

apply plugin: "de.undercouch.download"
apply plugin: 'base'

group 'intellij.tools'
version '1.0'

apply from: 'setupJdk.gradle'
apply from: 'setupAndroidSdk.gradle'
apply from: 'setupBuildScriptsDeps.gradle'
apply from: 'setupToolboxLiteGen.gradle'
apply from: 'setupBundledMaven.gradle'
apply from: 'setupCidr.gradle'
apply from: 'setupRpluginDeps.gradle'
apply from: 'setupAndroidStudio.gradle'
//Temporary unbundle VulnerabilitySearch
//apply from: 'setupVulnerabilitySearchPlugin.gradle'

task setupDependencies(dependsOn: ['setupJdks', 'setupBundledMaven'], group: 'dependencies')
clean.dependsOn('cleanSetupJdks', 'cleanSetupBundledMaven')

task dependenciesFile() {
  description = 'Creates file with all dependencies version, even those that were overloaded by system or env properties'
  def outputFile = "$project.buildDir/dependencies.properties"
  outputs.upToDateWhen { false }
  outputs.file(outputFile)
  doFirst {
    def properties = new Properties()
    file('gradle.properties').withInputStream { properties.load(it) }
    file(outputFile).text = properties.stringPropertyNames().collect { "$it=${project.ext[it]}" }.join('\n')
  }
}

wrapper {
  gradleVersion = '5.5'
  distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}