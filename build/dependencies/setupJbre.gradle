import org.gradle.internal.os.OperatingSystem

import java.util.concurrent.TimeUnit

task setupJbre()

def targetOs = System.getProperty("intellij.build.target.os", "all")
def jreVersion = System.getProperty("intellij.build.bundled.jre.version", "11").toInteger()
def jreBuild = System.getProperty("intellij.build.bundled.jre.build", jdkBuild)
def jrePrefix = System.getProperty("intellij.build.bundled.jre.prefix")
createJbreTasks(jreBuild, jreVersion, targetOs, jrePrefix)

if (System.getProperty('intellij.build.bundle.second.jre', 'false').toBoolean()) {
  def secondJreBuild = System.getProperty("intellij.build.bundled.second.jre.build", secondJreBuild)
  createJbreTasks(secondJreBuild, 8, targetOs)
}

/**
 * Update this method together with:
 *  `setupJdk.gradle` 
 *  `org/jetbrains/intellij/build/impl/BundledJreManager.groovy`
 */
static def splitJreBuild(String jreBuild, String version) {
  def split = jreBuild.split('b')
  if (split.length > 2) {
    throw new IllegalArgumentException(
      "$jreBuild is expected in format <update>b<build_number>. Examples: u202b1483.24, 11_0_2b140, b96"
    )
  }
  if (split.length == 2) {
    def update = split[0]
    if (update.startsWith(version)) update -= version
    // [11_0_2, b140] or [8u202, b1483.24]
    ["$version$update", "b${split[1]}"]
  }
  else {
    // [11, b96]
    [version, jreBuild]
  }
}

def createJbreTasks(String defaultBuild, int version, String targetOs, String prefix = null) {
  def isModular = version >= 9
  if (prefix == null) prefix = isModular ? 'jbr-' : 'jbrx-'
  jrePlatformsToDownload(targetOs).each { platform ->
    archToDownload(platform, !isModular).each { arch ->
      def jreBuild = project.ext.has("jreBuild_$platform") ? project.ext["jreBuild_$platform"] : defaultBuild
      def (update, buildNumber) = splitJreBuild(jreBuild.toString(), version.toString())
      def jbrxArtifactName = "$prefix$update-$platform-$arch-$buildNumber".toString()
      def download = createDownloadJbreTask(jbrxArtifactName, jreBuild.toString(), !isModular)
      if (!isModular) {
        def jbrArtifactName = "jbr-$update-$platform-$arch-$buildNumber"
        def untarTask = createUntarJbreTask(download, jbrxArtifactName.capitalize(), platform)
        createTarJbreTask("tar${jbrArtifactName.capitalize()}", untarTask, platform, jbrArtifactName, false)
        setupJbre.dependsOn("tar${jbrArtifactName.capitalize()}")
        cleanSetupJbre.dependsOn("cleanTar${jbrArtifactName.capitalize()}")
        createTarJbreTask("tar${jbrxArtifactName.capitalize()}", untarTask, platform, jbrxArtifactName, true)
        setupJbre.dependsOn("tar${jbrxArtifactName.capitalize()}")
        cleanSetupJbre.dependsOn("cleanTar${jbrxArtifactName.capitalize()}")
      }
    }
  }
}

@SuppressWarnings("GrMethodMayBeStatic")
def jreUrl() {
  // base url of TeamCity build to download JRE from
  System.getProperty('intellij.jre.teamcity.build.url') ?: 'https://cache-redirector.jetbrains.com/intellij-jbr'
}

def createDownloadJbreTask(String artifactName, String build, boolean doRepackage) {
  def outputDir = "$project.buildDir/jbre"
  def suffix = doRepackage ? '_origin' : ''
  def outputFile = "$outputDir/${artifactName}${suffix}.tar.gz"
  task("download${artifactName.capitalize()}") {
    inputs.property('build', build)
    outputs.file(outputFile)
    doLast {
      logger.info("Downloading $artifactName to $outputFile")
      download {
        src "${jreUrl()}/${artifactName}.tar.gz"
        dest outputFile
        tempAndMove true
        timeout TimeUnit.MINUTES.toMillis(30).toInteger()
      }
    }
    setupJbre.dependsOn(it)
    cleanSetupJbre.dependsOn("clean${it.name.capitalize()}")
  }
  file(outputFile)
}

def createUntarJbreTask(File downloadOutputs, String taskName, String platform) {
  Task untar
  if (OperatingSystem.current().isWindows()) {
    // note: tarTree extracts symlinks as 0 length files which breaks osx distributions 
    untar = task("untar$taskName", type: Copy, dependsOn: "download$taskName") {
      from tarTree(downloadOutputs), {
        exclude 'lib/tools.jar'
      }
      from tarTree(downloadOutputs), {
        include 'lib/tools.jar'
        into 'jre'
      }
      into { "${downloadOutputs.parent}/${downloadOutputs.name - '.tar.gz'}/" }
      includeEmptyDirs = false
    }
  }
  else {
    untar = task("untar$taskName", type: Exec, dependsOn: "download$taskName") {
      def outputDir = "${downloadOutputs.absolutePath - '.tar.gz'}"
      inputs.file(downloadOutputs)
      outputs.dir(outputDir)
      doFirst { exec { commandLine 'mkdir', '-p', outputDir } }
      commandLine 'tar', '-xpf', "${downloadOutputs.absolutePath}", '--directory', outputDir
      if (platform != 'osx') {
        doLast {
          if (file("$outputDir/lib/tools.jar").exists()) {
            exec {
              commandLine 'mv', "$outputDir/lib/tools.jar", "$outputDir/jre/lib/"
              ignoreExitValue = true
            }
          }
        }
      }
    }
  }
  cleanSetupJbre.dependsOn("cleanUntar$taskName")
  untar
}

def createTarJbreTask(String taskName,
                      Task untarTask,
                      String platform,
                      String archive,
                      boolean includeToolsJar) {
  def dirToTar = platform == 'osx' ? 'jdk' : 'jre'
  if (OperatingSystem.current().isWindows()) {
    task(taskName, type: Tar) {
      def untarOutputs = untarTask.outputs.files
      inputs.file(untarOutputs)
      from "$untarOutputs.singleFile/$dirToTar"
      if (!includeToolsJar) {
        exclude "**/tools.jar"
      }
      into dirToTar
      compression = Compression.GZIP
      archiveName = "${archive}.tar.gz"
      destinationDir = untarOutputs.singleFile.parentFile
    }
  }
  else {
    task(taskName, type: Exec) {
      def untarOutputs = untarTask.outputs.files
      def outputFile = "${untarOutputs.singleFile.parentFile}/${archive}.tar.gz"
      inputs.files(untarOutputs)
      outputs.file(outputFile)
      def arguments = ['tar', '-czf', outputFile, '-C', untarOutputs.singleFile.absolutePath]
      if (!includeToolsJar) {
        arguments += ['--exclude', '**/tools.jar']
        // exclude entire lib directory (IDEA-176641)
        if (platform == 'osx') {
          arguments += ['--exclude', './jdk/Contents/Home/lib']
        }
      }
      arguments += [dirToTar]
      commandLine = arguments
    }
  }
}

// see org.jetbrains.intellij.build.BuildOptions.targetOS
static def jrePlatformsToDownload(targetOs) {
  Set<String> jrePlatformToDownload = []
  if (targetOs == 'all' || targetOs == 'linux') jrePlatformToDownload += 'linux'
  if (targetOs == 'all' || targetOs == 'windows') jrePlatformToDownload += 'windows'
  if (targetOs == 'all' || targetOs == 'mac') jrePlatformToDownload += 'osx'
  jrePlatformToDownload
}

static def archToDownload(platform, is32BitArchSupported) {
  is32BitArchSupported && platform == 'windows' ? ['i586', 'x64'] : ['x64']
}