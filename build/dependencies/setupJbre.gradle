// Copyright 2000-2021 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
import java.util.concurrent.TimeUnit

apply from: 'archivesCleanup.gradle'
apply from: 'retries.gradle'

ext.jbreOutputDir = "$project.buildDir/jbre"
def deleteObsoleteJbreArchives = ext.deleteObsoleteArchivesTask('Jbre', jbreOutputDir)
task setupJbre() {
  finalizedBy deleteObsoleteJbreArchives
}

def targetOs = System.getProperty("intellij.build.target.os", "all")
def targetArch = System.getProperty("intellij.build.target.arch")
def jreVersion = System.getProperty("intellij.build.bundled.jre.version", "11").toInteger()
def jreBuild = System.getProperty("intellij.build.bundled.jre.build", runtimeBuild)
def forcedPrefix = System.getProperty("intellij.build.bundled.jre.prefix")
jbrTasks(jreBuild, jreVersion, targetOs, targetArch, forcedPrefix)

/**
 * Update this method together with:
 *  `setupJdk.gradle`
 *  `org/jetbrains/intellij/build/impl/BundledJreManager.groovy`
 *  com.jetbrains.gateway.downloader.CodeWithMeClientDownloader#downloadClientAndJdk(java.lang.String, java.lang.String, com.intellij.openapi.progress.ProgressIndicator)
 */
static def splitJbrBuild(String jreBuild, String version) {
  def split = jreBuild.split('b')
  if (split.length > 2) {
    throw new IllegalArgumentException(
      "$jreBuild is expected in format <update>b<build_number>. Examples: u202b1483.24, 11_0_2b140, b96"
    )
  }
  if (split.length == 2) {
    def update = split[0]
    if (update.startsWith(version)) update -= version
    // [11_0_2, b140] or [8u202, b1483.24]
    ["$version$update", "b${split[1]}"]
  }
  else {
    // [11, b96]
    [version, jreBuild]
  }
}

def jbrTasks(String defaultBuild, int version, String targetOs, String targetArch, String forcedPrefix) {
  platformsToDownload(targetOs).each { platform ->
    archToDownload(platform, targetArch, version).each { arch ->
      prefixes(arch, forcedPrefix).each { prefix ->
        def jbrBuild = project.ext.has("jreBuild_${platform}_${arch}") ? project.ext["jreBuild_${platform}_${arch}"] :
                       (project.ext.has("jreBuild_${platform}") ? project.ext["jreBuild_${platform}"] : defaultBuild)
        def (update, buildNumber) = splitJbrBuild(jbrBuild.toString(), version.toString())
        def jbrArtifactName = "$prefix$update-$platform-$arch-$buildNumber"
        downloadJbrTask(jbrArtifactName, jbrBuild.toString())
      }
    }
  }
}

def downloadJbrTask(String artifactName, String build) {
  def outputFile = "${jbreOutputDir}/${artifactName}.tar.gz"
  task("download${artifactName.capitalize()}") {
    inputs.property('build', build)
    outputs.file(outputFile)
    doLast {
      logger.info("Downloading $artifactName to $outputFile")
      project.ext.retry {
        download {
          src "https://cache-redirector.jetbrains.com/intellij-jbr/${artifactName}.tar.gz"
          dest outputFile
          tempAndMove true
          connectTimeout TimeUnit.SECONDS.toMillis(30).toInteger()
          readTimeout TimeUnit.MINUTES.toMillis(5).toInteger()
        }
      }
    }
    setupJbre.dependsOn(it)
    cleanSetupJbre.dependsOn("clean${it.name.capitalize()}")
  }
  file(outputFile)
}

// see org.jetbrains.intellij.build.BuildOptions.targetOS
static def platformsToDownload(targetOs) {
  Set<String> jrePlatformToDownload = []
  if (targetOs == 'all' || targetOs == 'linux') jrePlatformToDownload += 'linux'
  if (targetOs == 'all' || targetOs == 'windows') jrePlatformToDownload += 'windows'
  if (targetOs == 'all' || targetOs == 'mac') jrePlatformToDownload += 'osx'
  jrePlatformToDownload
}

enum Arch {
  x86, x64, aarch64
}

static def archToDownload(String platform, String targetArch, int jbrVersion) {
  if (targetArch != null) {
    return [Arch.valueOf(targetArch)]
  }
  if (platform == 'osx') {
    return [Arch.x64, Arch.aarch64]
  }
  if (jbrVersion > 11) {
    return [Arch.x64]
  }
  return [Arch.x86, Arch.x64]
}

/**
 * Update this method together with:
 *  `org/jetbrains/intellij/build/impl/BundledJreManager.groovy`
 */
static def prefixes(Arch arch, String forcedPrefix) {
  if (arch == Arch.x86) {
    ['jbr-']
  }
  else if (forcedPrefix != null) {
    [forcedPrefix]
  }
  else {
    ['jbr_dcevm-']
  }
}