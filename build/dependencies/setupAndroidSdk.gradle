/*
 * Copyright 2000-2019 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os
import org.gradle.util.GFileUtils
import javax.inject.Inject

repositories {
  maven { url 'https://cache-redirector.jetbrains.com/repo.maven.apache.org/maven2/' }
  maven { url 'https://cache-redirector.jetbrains.com/kotlin.bintray.com/kotlin-plugin/' }
  maven { url 'https://cache-redirector.jetbrains.com/dl.bintray.com/kotlin/kotlin-eap' }
  maven { url 'https://cache-redirector.jetbrains.com/dl.bintray.com/kotlin/kotlin-bootstrap' }
  maven { url 'https://cache-redirector.jetbrains.com/jetbrains.bintray.com/intellij-third-party-dependencies/' }
}

configurations {
  androidLayoutlib
  androidSdk
  kotlinDeps
}

def osFamily = 'linux'
if (Os.isFamily(Os.FAMILY_WINDOWS)){
  osFamily = 'windows'
} else if (Os.isFamily(Os.FAMILY_MAC)){
  osFamily = 'darwin'
}

// infer kotlin_version from kotlin-plugin (e.g. "1.4.10" in "203-1.4.10-release-IJ2965:Ideadev")
def kotlinVersionLen = kotlinPluginBuild.indexOf("-release")
if (kotlinVersionLen == -1) {
  kotlinVersionLen = kotlinPluginBuild.indexOf("-IJ")
}

ext.kotlin_version = kotlinPluginBuild.substring(
  4, // number of chars in "203-" string (or "202-" string or any other platform identifier with dash string)
  kotlinVersionLen
)
ext.android_sdk_version = "${osFamily}.4.0.0.0"
ext.android_layoutlib_version = "27.1.1.1"

dependencies {
  androidLayoutlib "org.jetbrains.intellij.deps.android.tools.base:layoutlib-resources:${android_layoutlib_version}"
  androidSdk "org.jetbrains.intellij.deps.android:android-sdk:${android_sdk_version}:@tar.gz"
  kotlinDeps "org.jetbrains.kotlin:kotlin-android-extensions:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-android-extensions-runtime:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-annotation-processing-gradle:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-build-common:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-compiler-embeddable:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-compiler-runner:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-daemon-embeddable:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-gradle-plugin-api:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-gradle-plugin-model:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-script-runtime:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-scripting-common:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-scripting-compiler-impl-embeddable:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-scripting-jvm:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-stdlib-common:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-test:${kotlin_version}"
  kotlinDeps "org.jetbrains.kotlin:kotlin-test-common:${kotlin_version}"
}

task syncAndroidSdk(dependsOn: [configurations.androidSdk, configurations.androidLayoutlib], type: SyncAndroidSdkTask) {
  CopyUnitOfWork.project = project
  work = [
    new CopyData().with {
      it.from = configurations.androidLayoutlib.name
      it.into = "${project.buildDir}/android-sdk/layoutlib"
      it.versionFile = android_layoutlib_version
      it
    }, new CopyData().with {
      it.from = configurations.androidSdk.name
      it.into = "$buildDir/android-sdk/prebuilts/studio/sdk"
      it.versionFile = android_sdk_version
      it
    },
  ]
  onlyIf {
    work.any {
      !it.versionFile.exists()
    }
  }
  doFirst {
    work.each {
      it.into.deleteDir()
    }
  }
}

task setupAndroidPluginRuntimeForIdea(dependsOn: [setupKotlinPlugin]){

}

task syncKotlinPluginForAndroid(dependsOn: [setupKotlinPlugin, syncAndroidSdk], type: Sync) {
  from "$buildDir/kotlin"
  into "$buildDir/android-sdk/prebuilts/tools/common/kotlin-plugin"
}

/**
 * This is entry point: invoke this task to setup android sdk and all the artifacts required to run android-plugin unit tests
 */
task setupAndroidSdk(dependsOn: [syncAndroidSdk, syncKotlinPluginForAndroid]) {

}

class CopyData implements Serializable {
  String from
  File into
  File versionFile
  void setInto(String into) {
    this.into = new File(into)
  }

  void setVersionFile(String versionStr) {
    this.versionFile = new File(into, "${versionStr}.version")
  }
}

/**
 * Worker API is an incubating feature and it has already been changed in newer Gradle versions. Have to be rewritten on upgrade
 */
class CopyUnitOfWork implements Runnable {
  static Project project
  CopyData parameters

  @Inject
  CopyUnitOfWork(CopyData parameters) {
    this.parameters = parameters
  }

  @Override
  void run() {
    extract(project.configurations."$parameters.from".singleFile as File)
    parameters.versionFile.createNewFile()
  }

  private enum FileType { ZIP, TAR }

  private FileType fileType(File archive) {
    if (archive.name.endsWith("tar.gz")) {
      FileType.TAR
    }
    else if (archive.name.endsWith("zip") || archive.name.endsWith("jar")) {
      FileType.ZIP
    }
    else {
      throw new Exception(archive.name)
    }
  }

  def extract(File archive) {
    def destination = parameters.into
    def fileType = fileType(archive)
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
      project.copy {
        into destination
        from {
          if (fileType == FileType.TAR) {
            project.tarTree(project.resources.gzip(archive))
          }
          else {
            project.zipTree(archive)
          }
        }
      }
    }
    else if (fileType == FileType.TAR) {
      untar(archive, destination)
    }
    else {
      unzip(archive, destination)
    }
  }

  static def unzip(File zip, File destination) {
    println 'Using native unzip'
    project.exec { commandLine 'mkdir', '-p', destination.absolutePath }
    project.exec { commandLine 'unzip', '-qq', zip.absolutePath, '-d', destination.absolutePath }
  }

  static def untar(File tar, File destination) {
    println 'Using native tar'
    project.exec { commandLine 'mkdir', '-p', destination.absolutePath }
    project.exec { commandLine 'tar', '-xpf', "${tar.absolutePath}", '--directory', destination.absolutePath }
  }
}

class SyncAndroidSdkTask extends DefaultTask {
  Collection<CopyData> work
  WorkerExecutor workerExecutor

  @Inject
  SyncAndroidSdkTask(WorkerExecutor workerExecutor) {
    this.workerExecutor = workerExecutor
  }

  @TaskAction
  void copyAll() {
    work.each { CopyData data ->
      workerExecutor.submit(CopyUnitOfWork.class) { WorkerConfiguration config ->
        config.isolationMode = IsolationMode.NONE
        config.params data
      }
    }
  }
}
