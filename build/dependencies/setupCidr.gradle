import org.gradle.internal.os.OperatingSystem
import java.util.concurrent.TimeUnit

final def IJ_DIR = project.file("../../..").canonicalFile
final def REPOSITORY = "https://repo.labs.intellij.net/clion"
final def SETUP_ALL_PLATFORMS = Boolean.getBoolean("intellij.cidr.setup.all.platforms")

final def CURRENT_PLATFORM = {
  def os = OperatingSystem.current()

  if (os.isWindows()) "win"
  else if (os.isMacOsX()) "mac"
  else "linux"
}()

final def PACKAGE_EXTENSIONS = [
  win: "zip",
  mac: "tar.gz",
  linux: "tar.gz"
]

task setupCidr

[
  clang: [
    version: "10.0.0.143e87",
    platforms: [
      win: [archs: ["x64"]],
      linux: [archs: ["x64"]],
      mac: [archs: ["x64"]]
    ],
    path: "CIDR/clion/bin/clang"
  ],
  lldb: [
    version: "e0cccbacab5cf5ee621edddca1ea452353192e6b",
    platforms: [
      win: [
        version: "1e0f8f649a3b18bcc2ddda7311cdb9b9faabd042",
        archs: ["x64", "x86"]
      ],
      linux: [archs: ["x64"]],
      mac: [archs: ["x64"]]
    ],
    path: "CIDR/cidr-debugger/bin/lldb"
  ],
  lldbfrontend: [
    version: "da55ae377f35fa2cd0c0e76af9ad648823edcf3b",
    platforms: [
      win: [archs: ["x64", "x86"]],
      linux: [archs: ["x64"]],
      mac: [archs: ["x64"]]
    ],
    path: "CIDR/cidr-debugger/bin/lldb"
  ],
  bridge: [
    version: "51b0c22",
    platforms: [
      mac: [archs: ["x64"]]
    ],
    path: "CIDR-appcode/appcode/bin",
    no_platform_subdir: true
  ],
  jbdevice: [
    version: "810660",
    platforms: [
      "mac": [archs: ["x64"]]
    ],
    path: "CIDR-appcode/appcode/bin",
    no_platform_subdir: true
  ],
  objchelper: [
    version: "e9b1fe3",
    platforms: [
      "mac": [archs: ["x64"]],
      "linux": [archs: ["x64"]]
    ],
    path: "CIDR-appcode/appcode/bin",
    no_platform_subdir: true
  ]
].each { name, settings ->
  settings.platforms.findAll {
    SETUP_ALL_PLATFORMS || it.key == CURRENT_PLATFORM
  }.each { platform, platformSettings ->
    def version = platformSettings.containsKey("version") ?
      platformSettings.version : settings.version

    platformSettings.archs.each { arch ->
      def taskSuffix = "${name.capitalize()}${platform.capitalize()}${arch.capitalize()}"
      def packageExtension = PACKAGE_EXTENSIONS[platform]


      def packageName = "${name}-${version}-${platform}-${arch}.${packageExtension}"
      def downloadUrl = "${REPOSITORY}/${name}/${version}/${packageName}"

      def unpackDir = "${IJ_DIR}/${settings.path}" +
                      "${settings.no_platform_subdir ? '' : "/${platform}"}" +
                      "${platformSettings.archs.size > 1 ? "/${arch}" : ''}"

      def downloadTool = task("downloadCidr${taskSuffix}") {
        def packageArchive = "${buildDir}/cidr/${packageName}"
        outputs.file(packageArchive)

        doLast {
          download {
            src downloadUrl
            dest packageArchive
            tempAndMove true
            connectTimeout TimeUnit.SECONDS.toMillis(30).toInteger()
            readTimeout TimeUnit.MINUTES.toMillis(5).toInteger()
            retries 10
          }
        }
      }

      setupCidr.dependsOn(
        tasks.create(
          name: "setupCidr${taskSuffix}",
          dependsOn: [downloadTool], type: Copy
        ) {
          def packageArchive = downloadTool.outputs.files.singleFile

          inputs.files(packageArchive)
          outputs.dir(unpackDir)

          outputs.upToDateWhen {
            !fileTree(unpackDir).isEmpty()
          }

          from {
            if (packageExtension == "zip")
              zipTree(packageArchive)
            else if(packageExtension == "tar.gz")
              tarTree(resources.gzip(packageArchive))
          }
          into unpackDir
        }
      )
    }
  }
}

task cleanSetupCidr() {
  doLast {
    setupCidr.dependsOn.each { setupToolTask ->
      def downloadToolTask = setupToolTask.dependsOn.first()
      delete setupToolTask.outputs.files.singleFile
      delete downloadToolTask.outputs.files.singleFile
    }
  }
}
