/*
 Copyright 2019 Thomas Rosenau

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 */

{
  parserClass="de.thomasrosenau.diffplugin.parser.DiffParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Diff"
  psiImplClassSuffix="Impl"
  psiPackage="de.thomasrosenau.diffplugin.psi"
  psiImplPackage="de.thomasrosenau.diffplugin.psi.impl"

  elementTypeHolderClass="de.thomasrosenau.diffplugin.psi.DiffTypes"
  elementTypeClass="de.thomasrosenau.diffplugin.psi.DiffElementType"
  tokenTypeClass="de.thomasrosenau.diffplugin.psi.DiffTokenType"
}

// TODO: make stuff private if possible

// https://www.gnu.org/software/diffutils/manual/diffutils.html#Imperfect
diffFile ::= (leadingText (normalDiff | contextDiff | unifiedDiff))+ trailingText // TODO: handle indentation

// TODO: Handle Prereq: and Index:
// TODO: Handle git header
leadingText ::= anyLine* (consoleCommand anyLine*)?
consoleCommand ::= COMMAND
// TODO: Handle git footer
trailingText ::= anyLine*
anyLine ::= WHITE_SPACE | OTHER

// https://www.gnu.org/software/diffutils/manual/diffutils.html#Detailed-Context
contextDiff ::= CONTEXT_FROM_LABEL CONTEXT_TO_LABEL contextHunk+
contextHunk ::= CONTEXT_HUNK_SEPERATOR contextHunkFrom contextHunkTo
contextHunkFrom ::= CONTEXT_FROM_LINE_NUMBERS contextFromFileLine* (EOLHINT)?
contextHunkTo ::= CONTEXT_TO_LINE_NUMBERS contextToFileLine* (EOLHINT)?
contextFromFileLine ::= CONTEXT_COMMON_LINE | CONTEXT_CHANGED_LINE | CONTEXT_DELETED_LINE
contextToFileLine ::= CONTEXT_COMMON_LINE | CONTEXT_CHANGED_LINE | CONTEXT_INSERTED_LINE

// https://www.gnu.org/software/diffutils/manual/diffutils.html#Detailed-Unified
unifiedDiff ::= UNIFIED_FROM_LABEL UNIFIED_TO_LABEL unifiedHunk+
// TODO: Handle git binaries
unifiedHunk ::= UNIFIED_LINE_NUMBERS (unifiedLine | WHITE_SPACE)+
unifiedLine ::= UNIFIED_INSERTED_LINE | UNIFIED_DELETED_LINE | UNIFIED_COMMON_LINE | EOLHINT

// https://www.gnu.org/software/diffutils/manual/diffutils.html#Detailed-Normal
normalDiff ::= normalHunk+
normalHunk ::= normalHunkAdd | normalHunkChange | normalHunkDelete
normalHunkAdd ::= NORMAL_ADD_COMMAND NORMAL_TO_LINE+ EOLHINT?
normalHunkChange ::= NORMAL_CHANGE_COMMAND NORMAL_FROM_LINE+ EOLHINT? NORMAL_SEPARATOR NORMAL_TO_LINE+ EOLHINT?
normalHunkDelete ::= NORMAL_DELETE_COMMAND NORMAL_FROM_LINE+ EOLHINT?
