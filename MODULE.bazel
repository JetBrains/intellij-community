module(name = "community")

bazel_dep(name = "bazel_features", version = "1.39.0")
bazel_dep(name = "bazel_skylib", version = "1.9.0")
bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "rules_kotlin", version = "2.2.2")
bazel_dep(name = "rules_java", version = "9.3.0")
bazel_dep(name = "rules_jvm", version = "0.0.1")
bazel_dep(name = "lib")
local_path_override(
    module_name = "lib",
    path = "lib",
)

local_path_override(
    module_name = "rules_jvm",
    path = "build/jvm-rules",
)

http_file = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_file")

bazel_dep(name = "toolchains_llvm", version = "1.6.0")
single_version_override(
    module_name = "toolchains_llvm",
    patch_strip = 1,
    patches = ["//build:toolchains_llvm-windows_support.patch"],  # Add Windows support, patch of https://github.com/bazel-contrib/toolchains_llvm/pull/642
)

llvm = use_extension("@toolchains_llvm//toolchain/extensions:llvm.bzl", "llvm", dev_dependency = True)  # `dev_dependency = True` to avoid leaking the extension in modules using that module, see https://github.com/bazel-contrib/toolchains_llvm/issues/569
llvm.toolchain(
    name = "llvm_toolchain",
    llvm_version = "20.1.3",
)

# No sysroot is wired as this MODULE.bazel is public

use_repo(llvm, "llvm_toolchain", "llvm_toolchain_llvm")

register_toolchains(
    "@llvm_toolchain//:all",
    dev_dependency = True,
)  # `dev_dependency = True` to avoid registering toolchains in modules using that module, see https://github.com/bazel-contrib/toolchains_llvm/issues/569

bazel_dep(name = "rules_python", version = "1.7.0")

kotlin_test_deps = use_repo_rule("//plugins/kotlin:kotlin_test_dependencies.bzl", "kotlin_test_deps")
kotlin_test_deps(name = "kotlin_test_deps")

http_file(
    name = "debugger_test_deps_debugger_agent",
    downloaded_file_path = "debugger-agent.jar",
    sha256 = "d08d14bf727e3a393dc9e0f95b86b3483d32c15027673324558a13d4eee3756e",
    url = "https://cache-redirector.jetbrains.com/packages.jetbrains.team/maven/p/ij/intellij-dependencies/org/jetbrains/intellij/deps/debugger-agent/1.133/debugger-agent-1.133.jar",
)

jbr_toolchains = use_extension("@community//build:jbr-toolchains.bzl", "jbr_toolchains")

REMOTE_JBR21_REPOS = ["remotejbr21_" + platform for platform in [
    "linux",
    "linux_aarch64",
    "macos",
    "macos_aarch64",
    "win",
    "win_arm64",
]]

[use_repo(
    jbr_toolchains,
    repo + "_toolchain_config_repo",
) for repo in REMOTE_JBR21_REPOS]

[register_toolchains("@" + repo + "_toolchain_config_repo//:all") for repo in REMOTE_JBR21_REPOS]

# Use custom remote_java_tools_windows with vc_redist .dll files due to https://github.com/bazelbuild/rules_java/issues/316
http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "remote_java_tools_windows_with_vc_redist",
    sha256 = "9ec4e41ebbb2b7259eb14f1ad708e8ee60087f33515bdec4876c675205e6950f",
    url = "https://packages.jetbrains.team/files/p/ij/intellij-build-dependencies/rules-java/java_tools_windows-v13.18-with-vc_redist.zip",
)

override_repo(
    use_extension("@rules_java//java:extensions.bzl", "toolchains"),
    remote_java_tools_windows = "remote_java_tools_windows_with_vc_redist",
)

# Register the extension that runs jps-to-bazel converter and provides target lists for dev-build.
# This creates the @jps_dynamic_deps_community repository.
use_repo(
    use_extension("//build:jps_dynamic_deps_community.bzl", "jps_dynamic_deps_community_extension"),
    "jps_dynamic_deps_community",
)
