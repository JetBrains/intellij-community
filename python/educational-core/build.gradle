import org.apache.tools.ant.filters.ReplaceTokens
buildscript {
  ext.kotlin_version = '1.0.5'
  repositories {
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
      url 'http://dl.bintray.com/jetbrains/intellij-plugin-service'
    }
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.2.0-SNAPSHOT"
  }
}

subprojects {

  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: "org.jetbrains.intellij"

  tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

  sourceSets {
    main {
      java.srcDir 'src'
      kotlin.srcDirs 'src'
      resources.srcDir 'resources'
    }
    test {
      java.srcDir 'test'
    }
  }

  intellij {
    String ideaPath = getIdeaPath()
    if (ideaPath == null) {
      version ideaVersion
    } else {
      localPath ideaPath
    }
    updateSinceUntilBuild Boolean.valueOf(updateBuildNumber)
    downloadSources Boolean.valueOf(downloadIdeaSources)
    sandboxDirectory = new File(rootProject.projectDir, "gradleBuild/idea-sandbox")
  }

  processResources {
    filesNotMatching("**/*.png") {
      it.filter(ReplaceTokens, tokens: [
        'VERSION'     : version.toString(),
        'BUILD-NUMBER': buildNumber,
      ])
    }
  }

  afterEvaluate {
    tasks.getByName('buildPlugin') {
      archiveName = "$intellij.pluginName-$version-${buildNumber}.zip"
    }
  }

  buildDir = new File(rootProject.projectDir, "gradleBuild/" + project.name)
}

private static String getIdeaPath() {
  Properties properties = new Properties()
  try {
    properties.load(new FileInputStream("local.properties"))
    return properties.getProperty('ideaPath')
  }
  catch (ignored) {
    return null
  }
}

project(':student') {

  dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar'])
  }

  intellij {
    pluginName 'student'
  }

}

project(':course-creator') {

  dependencies {
    compile project(':student')
  }

  intellij {
    pluginName 'course-creator'
  }

}