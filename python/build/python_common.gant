import groovy.io.FileType
import org.apache.tools.ant.util.StringUtils

private List<String> listTopLevelModules(String root) {
  def list = []

  def dir = new File(root)
  dir.eachFile (FileType.FILES) { file ->
    if (file.name.endsWith(".py")) {
       list << file.name - ".py"
    }
  }
  return list
}

binding.setVariable("buildDebuggerEgg", {String sandbox, String pythonHome, String buildName ->
  return buildEgg(sandbox, pythonHome, buildName, "pycharm-debug", {
    ant.fileset(dir: "$pythonHome/community/helpers/pydev") {
      include(name: "**/*.py")
    }
  })
})

binding.setVariable("buildDebuggerEggPy3K", {String sandbox, String pythonHome, String buildName ->
  return buildEgg(sandbox, pythonHome, buildName, "pycharm-debug-py3k") {
    ant.fileset(dir: "$pythonHome/community/helpers/pydev") {
      include(name: "**/*.py")
      exclude(name:"**/_pydev_BaseHTTPServer.py")
      exclude(name:"**/_pydev_SimpleXMLRPCServer.py")
      exclude(name:"**/_pydev_SocketServer.py")
      exclude(name:"**/_pydev_inspect.py")
      exclude(name:"**/_pydev_threading.py")
      exclude(name:"**/_pydev_xmlrpclib.py")
      exclude(name:"**/jyimportsTipper.py")
      exclude(name:"**/pydevconsole_code_for_ironpython.py")
      exclude(name:"**/pydevd_exec.py")
    }
  }
})

private GString buildEgg(String sandbox, String pythonHome, String buildName, String eggName, Closure custom) {
  def eggDir = "$sandbox/$eggName"
  def egg = "$sandbox/${eggName}.egg"

  ant.delete(dir: eggDir)
  ant.mkdir(dir: eggDir)

  ant.copy(todir: "$eggDir") {
      custom()
  }

  ant.mkdir(dir: "$eggDir/EGG-INFO")

  ant.copy(todir: "$eggDir/EGG-INFO") {
    fileset(dir: "$pythonHome/resources/debugger-egg/EGG-INFO")
  }

  ant.replace(file: "$eggDir/EGG-INFO/PKG-INFO") {
    replacefilter(token: "@@BUILD_NUMBER@@", value: buildName)
  }

  ant.replace(file: "$eggDir/pydevd_comm.py") {
    replacefilter(token: "@@BUILD_NUMBER@@", value: buildName)
  }

  ant.echo(file: "$eggDir/EGG-INFO/top_level.txt", message: listTopLevelModules("$eggDir").join(StringUtils.LINE_SEP))

  ant.zip(destfile: egg) {
    fileset(dir: eggDir)
  }
  return egg
}
