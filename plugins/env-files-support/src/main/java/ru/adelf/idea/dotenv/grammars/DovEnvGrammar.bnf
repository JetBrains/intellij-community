{
  parserClass="ru.adelf.idea.dotenv.parser.DotEnvParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="DotEnv"
  psiImplClassSuffix="Impl"
  psiPackage="ru.adelf.idea.dotenv.psi"
  psiImplPackage="ru.adelf.idea.dotenv.psi.impl"

  elementTypeHolderClass="ru.adelf.idea.dotenv.psi.DotEnvTypes"
  elementTypeClass="ru.adelf.idea.dotenv.psi.DotEnvElementType"
  tokenTypeClass="ru.adelf.idea.dotenv.psi.DotEnvTokenType"

  psiImplUtilClass="ru.adelf.idea.dotenv.psi.DotEnvPsiUtil"
}

dotEnvFile ::= item_*

private item_ ::= (EXPORT? property|COMMENT|CRLF)

key ::= KEY_CHARS
nested_variable_key ::= KEY_CHARS {
    name="property key"
    implements="com.intellij.model.psi.PsiExternalReferenceHost"
}

private quoted_value ::= (NESTED_VARIABLE_START nested_variable_key? NESTED_VARIABLE_END) | VALUE_CHARS

value ::= VALUE_CHARS+ | QUOTE quoted_value* QUOTE?

property ::= (key SEPARATOR value? COMMENT?) | key COMMENT? {
  methods=[getKeyText getValueText]
}
