apply plugin: 'maven-publish'

group = 'org.jetbrains.idea.maven'
version = '2023.2'

repositories {
  mavenCentral()
}

configurations {
  indexer
}

def MAVEN_INDEXER_VERSIONS = [
  "6.2.2"
]

dependencies {
  for (def v : MAVEN_INDEXER_VERSIONS) {
    indexer("org.apache.maven.indexer:indexer-core:$v")
  }
}

task copyIndexerDependencies(type: Copy) {
  group 'build'
  from configurations.indexer
  into file('build/tmp/indexer-jars')
  doFirst {
    file('build/tmp').deleteDir()
  }
}

task createMetaInfData() {
  group 'build'
  dependsOn 'copyIndexerDependencies'
  doLast {
    def dependenciesDir = file('build/tmp/indexer-jars')
    def metaDir = file("build/tmp/indexer-meta")
    metaDir.mkdirs()
    def metaFile = project.group + "." + project.name
    new File(metaDir, metaFile).text = dependenciesDir.list().join("\n")
  }
}


tasks.register('uberJar', Jar) {
  group 'build'
  dependsOn 'createMetaInfData'
  destinationDir file('build/dist')
  baseName rootProject.name + '-' + rootProject.version
  from file('build/tmp/indexer-jars')
  includeEmptyDirs false
  metaInf {
    from('build/tmp/indexer-meta') {
      include project.group + "." + project.name
    }
  }
}

def spaceUsername = project.getProperty('spaceUsername')
def spacePassword = project.getProperty('spacePasswordWrite')

publishing {
  publications {
    maven(MavenPublication) {
      artifact uberJar
      groupId = project.group
      artifactId = rootProject.name
      version = project.version
      suppressAllPomMetadataWarnings()
    }
  }
  repositories {
    maven {
      url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
      credentials {
        username = spaceUsername
        password = spacePassword
      }
    }
  }
}

