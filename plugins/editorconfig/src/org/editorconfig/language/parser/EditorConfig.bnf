// Copyright 2000-2018 JetBrains s.r.o. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
{
  parserClass="org.editorconfig.language.parser.EditorConfigParserBase"
  parserUtilClass="org.editorconfig.language.parser.EditorConfigParserUtil"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="EditorConfig"
  psiImplClassSuffix="Impl"
  psiPackage="org.editorconfig.language.psi"
  psiImplPackage="org.editorconfig.language.psi.impl"

  elementTypeHolderClass="org.editorconfig.language.psi.EditorConfigElementTypes"
  elementTypeClass="org.editorconfig.language.psi.EditorConfigElementType"
  tokenTypeClass="org.editorconfig.language.psi.EditorConfigTokenType"
  psiVisitorName="EditorConfigVisitor"
  psiImplUtilClass="org.editorconfig.language.psi.impl.EditorConfigPsiImplUtils"

  tokens=[
    LINE_COMMENT='regexp:[#;].*'
  ]

  extends(".*_pattern")=pattern
}

// ---- ---- Basic ---- ----

editorConfigFile ::= root_declaration* section_wrap*

private section_wrap ::= section <<unbindComments>>

section ::= header option* {
  pin=1
  recoverWhile=not_header
  mixin="org.editorconfig.language.psi.base.EditorConfigSectionBase"
  implements="com.intellij.psi.NavigatablePsiElement"
  methods = [
    containsKey
  ]
}

// ---- ---- Root declaration ---- ----

root_declaration ::= root_declaration_key SEPARATOR root_declaration_list (COLON root_declaration_list)? {
  pin=2
  recoverWhile=not_next_entry
  mixin="org.editorconfig.language.psi.base.EditorConfigRootDeclarationBase"
  implements="com.intellij.psi.NavigatablePsiElement"
  methods=[
    isValidRootDeclaration
  ]
}

private root_declaration_list ::= root_declaration_value (COMMA root_declaration_value)*

root_declaration_key ::= IDENTIFIER

root_declaration_value ::= IDENTIFIER

// ---- ---- Header ---- ----

header ::= L_BRACKET pattern_aux? R_BRACKET {
  pin=1
  recoverWhile=not_next_entry
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
  methods=[
    isValidGlob
  ]
}

fake pattern ::= {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}


private pattern_aux ::= (concatenated_pattern | elemental_pattern) {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

private elemental_pattern ::= (enumeration_pattern | flat_pattern | asterisk_pattern | double_asterisk_pattern | question_pattern | char_class_pattern)

concatenated_pattern ::= elemental_pattern (elemental_pattern)+ {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

enumeration_pattern ::= L_CURLY (pattern_aux (COMMA pattern_aux)*)? R_CURLY {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

asterisk_pattern ::= ASTERISK {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

double_asterisk_pattern ::= DOUBLE_ASTERISK {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

question_pattern ::= QUESTION {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

flat_pattern ::= (PATTERN_IDENTIFIER | PATTERN_WHITE_SPACE)+ {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

char_class_pattern ::= L_BRACKET char_class_exclamation? char_class_letter+ R_BRACKET {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

char_class_exclamation ::= EXCLAMATION {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
}

char_class_letter ::= CHARCLASS_LETTER {
  mixin="org.editorconfig.language.psi.base.EditorConfigHeaderElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigHeaderElement"
  methods=[
    isEscape
    isValidEscape
  ]
}

// ---- ---- Option ---- ----

option ::= option_with_raw_value | simple_option {
  mixin="org.editorconfig.language.psi.base.EditorConfigOptionBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigDescribableElement"
  methods=[
    getKeyParts
    getAnyValue

    // Cannot be deleted since it declares a different return type
    getDescriptor
  ]
}

private simple_option ::= option_key (SEPARATOR option_value?)? {
  pin=1
  recoverWhile=not_next_entry
}

private option_with_raw_value ::= <<isOptionWithRawValueKeyAhead>> option_key SEPARATOR <<rawOptionValue>>

// This line exists to generate the class for that node.
// The node itself is parsed manually
raw_option_value ::=

// it might be worth it to get rid of flat option key
// and treat it as qualified key with only one part
private option_key ::= qualified_option_key | flat_option_key

private option_value ::= option_value_pair | option_value_standalone_list | option_value_standalone_identifier

// ---- ---- Key ---- ----

qualified_option_key ::= DOT* (qualified_key_part DOT+)+ qualified_key_part? | (DOT+ qualified_key_part) {
  mixin="org.editorconfig.language.psi.base.EditorConfigQualifiedOptionKeyBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigDescribableElement"
}

qualified_key_part ::= IDENTIFIER {
  mixin="org.editorconfig.language.psi.base.EditorConfigQualifiedKeyPartBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigIdentifierElement"
}

flat_option_key ::= IDENTIFIER {
  mixin="org.editorconfig.language.psi.base.EditorConfigFlatOptionKeyBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigIdentifierElement"
  methods=[
    definesSameOption
    // Cannot be deleted since it declares a different return type
    getReference
  ]
}

// ---- ---- Value ---- ----

option_value_pair ::= (option_value_list | option_value_identifier) COLON (option_value_list | option_value_identifier) <<followedByNewLineOrEndOfFile>> {
  pin=2
  mixin="org.editorconfig.language.psi.base.EditorConfigDescribableElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigDescribableElement"
  methods=[
    getFirst
    getSecond

    getDescriptor
  ]
}

private option_value_standalone_list ::= option_value_list <<followedByNewLineOrEndOfFile>>

option_value_list ::= COMMA* option_value_identifier (COMMA option_value_identifier !(DOT | SEPARATOR) | COMMA)+ {
  mixin="org.editorconfig.language.psi.base.EditorConfigDescribableElementBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigDescribableElement"
  methods=[
    getDescriptor
  ]
}

private option_value_standalone_identifier ::= option_value_identifier <<followedByNewLineOrEndOfFile>>

option_value_identifier ::= IDENTIFIER {
  mixin="org.editorconfig.language.psi.base.EditorConfigOptionValueIdentifierBase"
  implements="org.editorconfig.language.psi.interfaces.EditorConfigIdentifierElement"
}

// ---- ---- Recovery ---- ----

private not_header ::= !header
private not_next_entry ::= !(header | option | root_declaration)
