buildscript {
    repositories {
        maven { url "https://www.jetbrains.com/intellij-repository/snapshots" } // for nightly builds
        if (Build.buildSettings.useCacheRedirector){
            maven { url "https://cache-redirector.jetbrains.com/jetbrains.bintray.com/intellij-plugin-service"}
        } else {
            maven { url "https://dl.bintray.com/jetbrains/intellij-plugin-service" }
        }
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.8"
}

applyKotlinJVM()

if (hasProperty("teamcity")) {
    version = teamcity["build.number"]
}

dependencies {
    compile project(":app:app-state")

    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-jetty:$ktor_version"
    compile "org.slf4j:jul-to-slf4j:$sl4j_version"
    compile "ch.qos.logback:logback-classic:1.2.1"

    compile "org.glassfish.jersey.core:jersey-common:2.23"
    compile "org.glassfish.jersey.core:jersey-client:2.23"

    compile "nl.komponents.kovenant:kovenant:3.3.0"
    compile "commons-io:commons-io:$apache_commons_io_version"

    compile project(':plugins:pipelines:pipelines-config:pipelines-config-dsl-compile')
    compile project(':plugins:pipelines:pipelines-config:pipelines-config-dsl-script-exec-common')
    compile project(':plugins:pipelines:pipelines-config:pipelines-config-utils')
    compile project(':plugins:pipelines:pipelines-common')

    compile project(':plugins:pipelines:pipelines-engine')
    compile project(':plugins:pipelines:pipelines-utils')
    compile project(':plugins:pipelines:pipelines-provider')
}

test { finalizedBy jacocoTestReport }

def kotlinIdeaPluginVersion = "1.3.50-release-IJ2019.2-1"

intellij {
    version "193.3519-EAP-CANDIDATE-SNAPSHOT"
    pluginName "space-intellij-plugin"
    updateSinceUntilBuild false
    sandboxDirectory "${project.buildDir}/.sandbox"
    setPlugins("org.jetbrains.kotlin:$kotlinIdeaPluginVersion", 'git4idea')
    downloadSources = true
}

runIde {
    jbrVersion = '11_0_2b159'
}

buildPlugin {
    doLast {
        def buildSettings = Build.buildSettings
        if (buildSettings.isCIRun()){
            def buildNumber = buildSettings.buildNumber

            new File("$projectDir/build/distributions/update.xml").write("""<!-- Plugin repository URL: "http://buildserver.labs.intellij.net/guestAuth/app/rest/builds/buildType:(id:Space_IdeaSpaceIntegration),tag:release/artifacts/content/update.xml" -->
<plugins>
    <plugin id="com.jetbrains.space" url="http://buildserver.labs.intellij.net/guestAuth/app/rest/builds/buildType:(id:Circlet_IdeaIntegration),number:${buildNumber}/artifacts/content/app-idea-${buildNumber}.zip" version="$buildNumber">
        <name>Space Integration</name>
        <description>Space Integration plugin</description>
    </plugin>
</plugins>""")
            println "##teamcity[publishArtifacts 'app/app-idea/build/distributions/update.xml']"
        }
    }
}
