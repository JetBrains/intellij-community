/*
 Copyright 2019 Thomas Rosenau

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
 */

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

plugins {
    id 'idea'
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.2'
    id 'org.jetbrains.grammarkit' version '2018.2.2'
}

group 'de.thomasrosenau'
version '0.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

intellij {
    version = 'LATEST-EAP-SNAPSHOT'
}

File genSrc = file('src/main/generated-java')
sourceSets.main.java.srcDir genSrc
idea.module.generatedSourceDirs += genSrc

def intellijPublishToken = project.properties['intellijPublishToken'] ?: "see http://www.jetbrains.org/intellij/sdk/docs/tutorials/build_system/deployment.html"

publishPlugin {
    token intellijPublishToken
    channels 'alpha'
}

task generateDiffLexer(type: GenerateLexer) {
    source 'src/main/java/de/thomasrosenau/diffplugin/lexer/Diff.flex'
    targetDir 'src/main/generated-java/de/thomasrosenau/diffplugin'
    targetClass 'DiffLexer'
    purgeOldFiles true
}

task generateDiffParser(type: GenerateParser) {
    source 'src/main/java/de/thomasrosenau/diffplugin/parser/Diff.bnf'
    targetRoot 'src/main/generated-java'
    pathToParser 'DiffParser.java'
    pathToPsiRoot 'psi'
    purgeOldFiles true
}
